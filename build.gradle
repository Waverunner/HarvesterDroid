// Build for all/sub-projects
allprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    version = '1.0.1'

    repositories {
        jcenter()
    }

    dependencies {
        // Use JUnit test framework
        testCompile 'junit:junit:4.12'
    }

    checkstyle {
        configFile = new File(rootDir, "checkstyle.xml")
        toolVersion ="8.1"
    }
}

subprojects {
    group = 'io.github.waverunner.harvesterdroid'

    jar {
        appendix = 'haversterdroid'
    }
}

// Applies to main application only

group = 'io.github.waverunner'
description = 'Manage your resources across a number of Star Wars Galaxies resource trackers'

apply plugin: 'javafx-gradle-plugin'
apply plugin: 'idea'

buildscript {
    dependencies {
        classpath 'de.dynamicfiles.projects.gradle.plugins:javafx-gradle-plugin:8.8.2'
    }

    repositories {
        jcenter()
    }
}

dependencies {
    compile project(':api')
    compile project(':loader')
    // TODO Move compiled tracker to trackers/galaxy-harvester.jar
    compile project(':tracker-gh')
    compile 'de.saxsys:mvvmfx:1.6.0'
    compile 'de.saxsys:mvvmfx-easydi:1.6.0'
    compile 'org.controlsfx:controlsfx:8.40.13'
    compile 'javax.persistence:persistence-api:1.0.2'
    compile 'com.vladsch.flexmark:flexmark:0.22.22'
    compile 'org.mongodb:mongodb-driver:3.5.0'
}

jfx {
    // minimal requirement for jfxJar-task
    jfxMainAppJarName = 'harvesterdroid.jar'
    mainClass = 'io.github.waverunner.harvesterdroid.Launcher'
    libFolderName = 'lib'

    // gradle jfxJar
    preLoader = 'io.github.waverunner.harvesterdroid.LauncherPreloader' // String

    // gradle jfxNative
    vendor = 'Waverunner'
    bundleArguments = [
            runtime: null
    ]
}
build.finalizedBy('jfxJar')

idea {
    module {
        downloadJavadoc = true
    }
}
